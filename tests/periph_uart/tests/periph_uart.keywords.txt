*** Settings ***
Library             UartDevice  port=%{PORT}  baudrate=%{BAUD}  timeout=${0.5}
Library             periph_uart_if.PeriphUartUtil

Resource            api_shell.keywords.txt
Resource            philip.keywords.txt

*** Keywords ***
DUT Should Match String
    [Arguments]                 ${test_string}  ${reference_string}
    ${RESULT}=                  Run Keyword  Uart Send String   test_string=${test_string}
    ${stat}=                    Get PHILIP Statistics
    ${err_msg}=                 Message for should match        ${RESULT}   ${reference_string}   ${stat}
    Should Be Empty             ${err_msg}   ${err_msg}

DUT Should Not Match String or Timeout
    [Arguments]                 ${test_string}  ${reference_string}
    ${RESULT}=                  Run Keyword  Uart Send String   test_string=${test_string}
    ${stat}=                    Get PHILIP Statistics
    ${err_msg}=                 Message for should not match    ${RESULT}   ${reference_string}   ${stat}
    #Should Be True              '${RESULT['result']}'=='Timeout' or '${rcv_string}'!='${reference_string}'   msg=${stat}
    Should Be Empty             ${err_msg}   ${err_msg}

DUT UART mode should exist
    ${status}   ${value}=       Run Keyword And Ignore Error   API Call Should Succeed   Uart Mode
    Pass Execution If           '${status}'=='FAIL'   Feature is not supported

DUT Uart Mode Change Should Succeed
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init
    API Call Should Succeed     Uart Mode   @{args}  &{kwargs}
    Uart Send string            flush

DUT Uart Init and Flush Should Succeed
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init   @{args}  &{kwargs}
    Uart Send string            flush
