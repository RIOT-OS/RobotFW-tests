*** Settings ***
Library             UartDevice  port=%{PORT}  baudrate=%{BAUD}  timeout=${%{HIL_CMD_TIMEOUT}}  connect_wait=${%{HIL_CONNECT_WAIT}}

Resource            api_shell.keywords.txt
Resource            philip.keywords.txt
Resource            riot_base.keywords.txt

*** Keywords ***
UART Flush
    [Documentation]             Remove garbage from UART buffer
    UART write  %{HIL_UART_DEV}  flush

UART Init and Flush Should Succeed
    [Documentation]             Init UART device and flush buffer
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init  %{HIL_UART_DEV}  @{args}  &{kwargs}
    UART Flush

UART Mode Change Should Succeed
    [Documentation]             Configure UART mode and flush
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init  %{HIL_UART_DEV}
    API Call Should Succeed     Uart Mode  %{HIL_UART_DEV}  @{args}  &{kwargs}
    UART Flush

UART Write Should Succeed
    [Documentation]             Write string to RF_UART_DEV should succeed
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Write  %{HIL_UART_DEV}  @{args}  &{kwargs}

UART Write Should Timeout
    [Documentation]             Write string to RF_UART_DEV should timeout
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Timeout     Uart Write  %{HIL_UART_DEV}  @{args}  &{kwargs}

UART Mode Should Exist
    [Documentation]             Verify DUT supports UART mode configuration
    ${status}   ${value}=       Run Keyword And Ignore Error   API Call Should Succeed   Uart Mode  %{HIL_UART_DEV}
    Run Keyword If              '${status}'=='FAIL'  UART Mode not supported

UART Mode not supported
    Fail  This mode is not supported for this board  non-critical

PHILIP Setup UART
    [Documentation]             Setup uart parameters on PHiLIP
    [Arguments]                 @{args}  &{kwargs}
    ${RESULT}=                  PHILIP.Setup Uart  @{args}  &{kwargs}
    Log                         ${RESULT}
